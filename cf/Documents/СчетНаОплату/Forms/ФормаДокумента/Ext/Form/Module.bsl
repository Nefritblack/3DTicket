
&Насервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ 		Задача, Пользователь Исполнитель, СекундОстаток Секунд, СуммаОстаток Сумма
	|ИЗ 			РегистрНакопления.ЗатраченноеВремя.Остатки(,Проект = &Проект)
	|УПОРЯДОЧИТь ПО Задача.Наименование
	|");
	
	Запрос.УстановитьПараметр("Проект", Объект.Проект);
	Объект.Задачи.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
	Модифицированность = Истина;
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодвал()
	
	Строки = Объект.Задачи.НайтиСтроки(Новый Структура("Согласовано", Истина));
	
	Сумма = 0; Секунд = 0; Строк = 0;
	Для Каждого Строка Из Строки Цикл 
		Сумма = Сумма + Строка.Сумма;
		Секунд = Секунд + Строка.Секунд; КонецЦикла;
	
	Элементы.ЗадачиСумма.ТекстПодвала 		= Сумма;
	Элементы.ЗадачиСекунд.ТекстПодвала 		= ОбщиеФункции.представлениеВремени(Секунд);
	Элементы.ЗадачиНомерСтроки.ТекстПодвала = Строки.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСогласовано(Согласовано)
	
	Для Каждого Строка Из Объект.Задачи Цикл Строка.Согласовано = Согласовано КонецЦикла;
	Модифицированность = Истина;
	ОбновитьПодвал();
	
КонецПроцедуры
&НаКлиенте
Процедура ВсеНеСогласовано(Команда)
	
	УстановитьСогласовано(Ложь);
	
КонецПроцедуры
&НаКлиенте
Процедура ВсеСогласовано(Команда)
	
	УстановитьСогласовано(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиПриИзменении(Элемент)
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПодвал();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Права.МожетСогласовыватьСчет() И объект.Операция = Перечисления.ОперацияСчета.НаСогласовании Тогда
		
		Элементы.Задачи.ИзменятьСоставСтрок = Ложь;
		Элементы.ЗадачиЗаполнить.Видимость = Ложь;
		
		Для Каждого Элемент Из Элементы Цикл
			Если СтрНачинаетсяС(Элемент.Имя, "Задачи") И ТипЗнч(Элемент) = Тип("ПолеФормы") И Элемент.Имя <> "ЗадачиСогласовано" Тогда
				Элемент.ТолькоПросмотр = Истина; КонецЕсли; КонецЦикла;
		
	ИначеЕсли Не Права.root() Тогда
		
		Элементы.Дата.Доступность 				= Ложь;
		Элементы.ЗадачиЗаполнить.Доступность 	= Ложь;
		Элементы.КнопкиСогласования.Доступность = Ложь;
		ТолькоПросмотр = Истина;  КонецЕсли;
	
КонецПроцедуры
