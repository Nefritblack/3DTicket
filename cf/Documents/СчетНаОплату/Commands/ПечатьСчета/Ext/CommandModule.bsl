
&НаСервере
Функция получитьданныеСчета(Ссылка)
	
	Запрос = Новый Запрос("ВЫБРАТЬ 0 НомерСтроки, Задача, Секунд, ""--------------"" Время, Сумма ИЗ Документ.СчетНаОплату.Задачи ГДЕ Ссылка = &Ссылка И Согласовано");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	таб = Запрос.Выполнить().Выгрузить();
	
	Ном = 0;
	Для Каждого Строка Из таб Цикл Ном = Ном + 1; 
		Строка.НомерСтроки 	= Ном;
		Строка.Время 		= ОбщиеФункции.представлениеВремени(Строка.Секунд); КонецЦикла;
	
	Возврат таб;
	
КонецФункции

&НаСервере
Функция ПолучитьСчет(Ссылка)
	
	ТабДок 	= Новый ТабличныйДокумент;
	Макет 	= Документы.СчетНаОплату.ПолучитьМакет("Счет");
	
	ОбластьШапка 		= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 		= Макет.ПолучитьОбласть("Строка");
	ОбластьСтрока1 		= Макет.ПолучитьОбласть("Строка1");
	ОбластьПодвал 		= Макет.ПолучитьОбласть("Подвал");
	
	ОбластьШапка.Параметры.ТекстЗаголовка 	= "Счет по выполненным работам №" + Ссылка.Номер + " от " + Формат(Ссылка.Дата, "ДЛФ=DD");
	ОбластьШапка.Параметры.Месяц 			= Ссылка.ПериодОплаты;
	ОбластьШапка.Параметры.Исполнитель 		= Ссылка.Исполнитель;
	ТабДок.Вывести(ОбластьШапка);
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	
	Данные = получитьданныеСчета(Ссылка);
	Для Каждого Строка Из Данные Цикл 
		Область = ?(Строка.НомерСтроки % 2, ОбластьСтрока, ОбластьСтрока1);
		Область.Параметры.Заполнить(Строка);
		ТабДок.Вывести(Область); КонецЦикла;
	
	ОбластьПодвал.Параметры.Время = ОбщиеФункции.представлениеВремени(Данные.Итог("Секунд"));
	ОбластьПодвал.Параметры.Сумма = "р." + Данные.Итог("Сумма");
	ТабДок.Вывести(ОбластьПодвал);
	
	Возврат ТабДок;
	
КонецФункции

&НаКлиенте
Процедура установитьпараметрыпечати(табДок)
	
	табДок.ОтображатьСетку 		= Ложь;
	табДок.ОтображатьЗаголовки 	= Ложь;
	
	табДок.НижнийКолонтитул.Выводить 	= Истина;
	табДок.НижнийКолонтитул.Шрифт 		= новый Шрифт("Arial", 8);
	табДок.НижнийКолонтитул.ТекстСправа = "подробности на http://3dticket.ru/1c";
	
КонецПроцедуры
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Для Каждого Ссылка Из ПараметрКоманды Цикл 
		ТабДок = ПолучитьСчет(Ссылка);
		установитьпараметрыпечати(табДок);
		ТабДок.Показать(Ссылка) КонецЦикла;
	
КонецПроцедуры
