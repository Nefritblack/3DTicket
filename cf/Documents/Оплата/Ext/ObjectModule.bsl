
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанных = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанных = Тип("Структура") Тогда
		КонвертацияТипов.ЗаполнитьОбъектПоСтруктуреОснованию(ЭтотОбъект, ДанныеЗаполнения); 
	ИначеЕсли  ТипДанных = Тип("ДокументСсылка.СчетНаОплату") Тогда
		Проект = ДанныеЗаполнения.Проект;
		Для Каждого Строка Из ДанныеЗаполнения.Задачи Цикл ЗаполнитьЗначенияСвойств(Задачи.Добавить(), Строка) КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОплаченноеВремя.Очистить();
	Движения.ПодтвержденноеВремя.Очистить();
	
	Для Каждого Строка Из Задачи Цикл
	
		Запись = Движения.ОплаченноеВремя.Добавить();
		Запись.Период 		= Дата;
		Запись.Проект 		= Проект;
		Запись.Пользователь = Строка.Исполнитель;
		Запись.Задача 		= Строка.Задача;
		Запись.Сумма 		= Строка.Сумма;
		
		Запись = Движения.ПодтвержденноеВремя.Добавить();
		Запись.ВидДвижения 	= ВидДвиженияНакопления.Расход;
		Запись.Период 		= Дата;
		Запись.Проект 		= Проект;
		Запись.Пользователь = Строка.Исполнитель;
		Запись.Задача 		= Строка.Задача;
		Запись.Сумма 		= Строка.Сумма; КонецЦикла;
	
	Движения.ОплаченноеВремя.Записать();
	Движения.ПодтвержденноеВремя.Записать();
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = Проект.Префикс;
	
КонецПроцедуры
